# 1. ERC-20スマートコントラクトを書く <a id="1-writing-erc-20-smart-contract"></a>

## 1.1 MyERC20 の全体構造 <a id="1-1-overall-structure-of-myerc20"></a>

`MyERC20.sol` の完全なソースコードを以下に示します。 この実装では、 `constructor` は `_mint` を呼び出し、コントラクトのデプロイ時にあらかじめ定義された量のトークンをミントします。

```text
pragma solidity ^0.5.0;

/**
 * @dev EIP で定義されているERC20 標準のインターフェイス。 オプションの関数は
 * を含まない; アクセスするには `ERC20Detailed` を参照してください。
 */
interface IERC20 {
    function totalSupply() external view returns (uint256);

    function balanceOf(address account) external view returns (uint256);

    function transfer(address recipient, uint256 amount) external returns (bool);

    function allowance(address owner, address spender) external view returns (uint256);

    function approve(address spender, uint256 amount) external returns (bool);

    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint256 value);

    event Approval(address indexed owner, address indexed spender, uint256 value);
}

library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Solidityの`+`演算子にカウンターパート。
     *
     * 要件:
     * - 追加はオーバーフローできません。
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Solidity の `-` 演算子に反応します。
     *
     * 要件:
     * - 減算はオーバーフローできません。
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a, "SafeMath: subtraction overflow");
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Solidity の `*` 演算子に反応します。
     *
     * 要件:
     * - 乗算はオーバーフローできません。
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers. 
     * の除算を 0 で戻します。 結果はゼロに丸められます。
     *
     * Solidityの`/`演算子にカウンターパート。 注意: この関数は
     * `revert` opcode (残りのガスをそのまま残す) を使用し、Solidity
     * は無効な opcode を使用して元に戻す (残りのガスを消費します) を使用します。
     *
     * 要件:
     * - 除数はゼロにはできません。
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        // Solidity only automatically asserts when dividing by 0
        require(b > 0, "SafeMath: division by zero");
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * 0 で割った時に元に戻します。
     *
     * Solidityの`%`演算子に反応します。 この関数は `revert`
     * opcode (残りのガスをそのまま残す) を使用し、Solidity は
     * 無効な opcode を使用して revert (残りのガスをすべて消費する) を使用します。
     *
     * 要件:
     * - 除数はゼロにはできません。
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b != 0, "SafeMath: modulo by zero");
        return a % b;
    }
}

/**
 * @dev Implementation of the `IERC20` interface.
 *
 * この実装はトークンの作成方法に依存しません。 これは、 `_mint` を使用して派生コントラクトに供給メカニズムを追加しなければならないことを意味する
*。
 * 一般的なメカニズムについては、 `ERC20Mintable` を参照してください。
 *
 * *For a detailed writeup see our guide [How to implement supply
 * mechanisms](https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226).*
 *
 * We have followed general OpenZeppelin guidelines: functions revert instead
 * of returning `false` on failure. それにもかかわらず、この挙動は従来の
 *であり、ERC20アプリケーションの期待と矛盾しない。
 *
 * 加えて、 `transferFrom` への呼び出し時に `Approval` イベントが発生します。
 * これにより、アプリケーションは、上記のイベントを聞くことによって、すべてのアカウントの手当を単に
 *を再構築することができます。 EIP の他の実装は、仕様で要求されていないため、これらのイベントを
 発行してはならない。
 *
 * 最後に、非標準の `decluseAllowance` と `increaseAllowance`
 * 関数が追加されました。
 * 許容値の設定に関するよく知られた問題を軽減します。 `IERC20.approve` を参照してください。
 */
contract MyERC20 is IERC20 {
    using SafeMath for uint256;

    mapping (address => uint256) private _balances;

    mapping (address => mapping (address => uint256)) private _allowances;

    // NOTE Start of https://github.com/OpenZeppelin/openzeppelin-solidity/blob/v2.3.0/contracts/token/ERC20/ERC20Detailed.sol
    string private _name;
    string private _symbol;
    uint8 private _decimals;

    constructor (string memory name, string memory symbol, uint8 decimals) public {
        _name = name;
        _symbol = symbol;
        _decimals = decimals;

        _mint(msg.sender, 100000 * 10 ** uint256(decimals)); // CAUTION!
    }

    /**
     * @dev トークンの名前を返します。
     */
    function name() public view returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * 例えば、`decimals`が`2`に等しい場合。 `505` トークンの残高は
     * `5,05` (`505 / 10 ** 2`)としてユーザーに表示されます。
     *
     * トークンは通常、
     * EtherとWeiの関係を模倣して、18の値を選択します。
     *
     * > Note that this information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * `IERC20.balanceOf` and `IERC20.transfer`.
     */
    function decimals() public view returns (uint8) {
        return _decimals;
    }
    // NOTE End of https://github.com/OpenZeppelin/openzeppelin-solidity/blob/v2.3.0/contracts/token/ERC20/ERC20Detailed.sol

    uint256 private _totalSupply;

    /**
     * @dev See `IERC20.totalSupply`.
     */
    function totalSupply() public view returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See `IERC20.balanceOf` .
     */
    function balanceOf(address account) public view returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev `IERC20.transfer` を参照してください。
     *
     * 要件:
     *
     * - `受信者`はゼロアドレスにはできません。
     * - 発信者は少なくとも `amount` の残高が必要です。
     */
    function transfer(address recipient, uint256 amount) public returns (bool) {
        _transfer(msg.sender, recipient, amount);
        return true;
    }

    /**
     * @dev See `IERC20.allowance`.
     */
    function allowance(address owner, address spender) public view returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See `IERC20.approve`.
     *
     * 要件:
     *
     * - `spender`はゼロアドレスにはできません。
     */
    function approve(address spender, uint256 value) public returns (bool) {
        _approve(msg.sender, spender, value);
        return true;
    }

    /**
     * @dev See `IERC20.transferFrom`.
     *
     * 更新された手当を示す `Approval` イベントを発行します。 これはEIPで要求される
     *ではありません。 `ERC20`の先頭にあるメモを参照してください。
     *
     * 要件:
     * - `sender` と `recipient` はゼロアドレスにはできません。
     * - `sender` には少なくとも `value` の残高が必要です。
     * - 発信者は、少なくとも
     * `amount` の `sender` トークンを許可している必要があります。
     */
    function transferFrom(address sender, address recipient, uint256 amount) public returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(sender, msg.sender, _allowances[sender][msg.sender].sub(amount));
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * これは、`IERC20.approve` で説明されている
     * 問題の緩和策として使用できる `approve` の代わりに使用します。
     *
     * 更新された手当を示す `Approval` イベントを発行します。
     *
     * 要件:
     *
     * - `spender`はゼロアドレスにはできません。
     */
    function increaseAllowance(address spender, uint256 addedValue) public returns (bool) {
        _approve(msg.sender, spender, _allowances[msg.sender][spender].add(addedValue));
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * これは、`IERC20.approve` で説明されている
     * 問題の緩和策として使用できる `approve` の代わりに使用します。
     *
     * 更新された手当を示す `Approval` イベントを発行します。
     *
     * 要件:
     *
     * - `spender`はゼロアドレスにはできません。
     * - `spender` は少なくとも
     * `subtractedValue`の呼び出しの許容値を持っている必要があります。
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public returns (bool) {
        _approve(msg.sender, spender, _allowances[msg.sender][spender].sub(subtractedValue));
        return true;
    }

    /**
     * @dev Moves tokens `amount` from `sender` to `recipient`.
     *
     * This is internal function is equivalent to `transfer`, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a `Transfer` event.
     *
     * 要件:
     *
     * - `sender` はゼロアドレスにはできません。
     * - `受信者`はゼロアドレスにはできません。
     * - `sender` は少なくとも `amount` の残高が必要です。
     */
    function _transfer(address sender, address recipient, uint256 amount) internal {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _balances[sender] = _balances[sender].sub(amount);
        _balances[recipient] = _balances[recipient].add(amount);
        emit Transfer(sender, recipient, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * `from` がゼロアドレスに設定された `Transfer` イベントを出力します。
     *
     * Requirements
     *
     * - `to` はゼロアドレスにはできません。
     */
    function _mint(address account, uint256 amount) internal {
        require(account != address(0), "ERC20: mint to the zero address");

        _totalSupply = _totalSupply.add(amount);
        _balances[account] = _balances[account].add(amount);
        emit Transfer(address(0), account, amount);
    }

     /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * `to` がゼロアドレスに設定された `Transfer` イベントを出力します。
     *
     * Requirements
     *
     * - `account` はゼロアドレスにはできません。
     * - `account` には少なくとも `amount` トークンが必要です。
     */
    function _burn(address account, uint256 value) internal {
        require(account != address(0), "ERC20: burn from the zero address");

    _balances[account] = _balances[account].sub(value);
        _totalSupply = _totalSupply.sub(value);
        emit Transfer(account, address(0), value);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner`s tokens.
     *
     * これは内部関数で `approve` と等価です と 
 * 例えば、 
 *
     *
     *
     * `Approval` イベントを発行します。
     *
     * 要件:
     *
     * - `owner`はゼロアドレスにはできません。
     * - `spender` はゼロアドレスにすることはできません。
     */
    function _approve(address owner, address spender, uint256 value) internal {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = value;
        emit Approval(owner, spender, value);
    }

    /**
     * @dev Destoys `amount` tokens from `account`.`amount` is then deducted
     * from the caller's allowance.
     *
     * `_burn` と `_approve` を参照。
     */
    function _burnFrom(address account, uint256 amount) internal {
        _burn(account, amount);
        _approve(account, msg.sender, _allowances[account][msg.sender].sub(amount));
    }
}
```

`MyERC20.sol` consists of one interface `IERC20`, one library `SafeMath` and one contract `MyERC20` which implements `IERC20` interface.

* `IERC20` インターフェイスは、 [ERC-20 仕様](https://eips.ethereum.org/EIPS/eip-20) に記述されている必須インターフェイスを定義します。
* `SafeMath` ライブラリは、固体の `uint256` タイプの安全な計算のためのオーバーフローチェックを追加した、固体の算術演算のラッパーを定義します。
* `MyERC20` は `IERC20` インターフェイスを実装しており、 [ERC-20 仕様](https://eips.ethereum.org/EIPS/eip-20) で記述される3つのオプションメソッドも定義しています。
  * ERC20に加えて、 `コンストラクタ` が定義され、このコンストラクタは新しいERC20トークン名とシンボルを定義するために使用されます。 事前に定義されたトークンをミントすることができます `コンストラクタ` は最初のデプロイ時に1回呼び出される。

## 1.2 重要な方法を見てみましょう <a id="1-2-take-a-look-at-important-methods"></a>

いくつかの重要な方法を詳しく見てみましょう。

### \(1\) `関数balanceOf(address account) external view return (uint256);` <a id="1-function-balanceof-address-account-external-view-returns-uint256"></a>

`` のバランスはERC-20の必須方法である。 `balanceOf` は指定されたアドレスの残高を返します。

```text
    function balanceOf(address account) public view returns (uint256) {
        return _balances[account];
}
```

`balanceOf` just returns of value of key `account` stored in `_balance` which is `mapping (address => uint256)` type as below.

```text
    マッピング (address => uint256) private _balances;
```

`_balances` で利用可能なキー `アカウント`がない場合は、 `0` を返します。

### \(2\) `関数transfer(アドレスの受信者、uint256 amount) external returns (bool);` <a id="2-function-transfer-address-recipient-uint256-amount-external-returns-bool"></a>

`transfer` はERC-20の必須メソッドです。 `transfer` tokens `amount` of tokens to `recipient`, and MUST(MUST) have activate the `Transfer` イベント. この関数は、メッセージ発信者のアカウント残高に使うのに十分なトークンがない場合にスローする必要があります(SHOULD)。

`transfer` は以下のように実際のtransfer と event を実装する内部メソッド `_transfer` を呼び出します。

```text
    function transfer(address recipient, uint256 amount) public returns (bool) {
        _transfer(msg.sender, recipient, amount);
        return true;
}
```

`_transfer` はERC-20 の `transfer` メソッドの実際の動作を実装しています。

さらに、以下のように `require` を使用してトークンを送信したり、ゼロアドレスへの送信を防ぎます。

```text
    function _transfer(address sender, address recipient, uint256 amount) internal {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _balances[sender] = _balances[sender].sub(amount);
        _balances[recipient] = _balances[recipient].add(amount);
        emit Transfer(sender, recipient, amount);
    }
```

### \(3\) `関数approve(address spender, uint256 amount) external returns (bool);` <a id="3-function-approve-address-spender-uint256-amount-external-returns-bool"></a>

`承認` はERC-20の必須メソッドです。 `` を承認すると、 `支出` が `金額`までの間、アカウントから複数回引き出すことができます。 この関数が複数回呼び出された場合は、単純に `amount` にリセットします。

`承認` は `承認` の実際の動作を実装する `内部メソッド` を呼び出すだけです。 `msg.sender` はアカウント `所有者` として渡されます。

```text
    function approve(address spender, uint256 value) public returns (bool) {
        _approve(msg.sender, spender, value);
        return true;
    }

    function _approve(address owner, address spender, uint256 value) internal {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = value;
        emit Approval(owner, spender, value);
    }
```

`_approvy` updates `_allowances` は、特定の `アドレスから` `spender` に許可された `値を維持する` 次元辞書です。

```text
    マッピング (address => mapping (address => uint256)) private _allowances;
```

### \(4\) `関数 _mint(address account, uint256 amount) 内部` <a id="4-function-_mint-address-account-uint256-amount-internal"></a>

`_mint` は ERC-20 の一部ではありません。 しかし、新しいERC-20トークンを作成し、この実装で新しいトークンを作成するために `_mint` を導入する方法が必要です。

```text
    function _mint(address account, uint256 amount) internal {
        require(account != address(0), "ERC20: mint to the zero address");

        _totalSupply = _totalSupply.add(amount);
        _balances[account] = _balances[account].add(amount);
        emit Transfer(address(0), account, amount);
    }
```

`_mint` は内部メソッドであり、このコントラクト内で呼び出すことができます。

In `MyERC20.sol`, `_mint` is invoked only once from `constructor` when deploying the smart contract to mint a predefined amount of token.

スマートコントラクトをデプロイした後に追加のトークンを発行する場合。 `mint` のような新しいパブリックメソッドを導入する必要があります。 許可されたユーザーのみがトークンを鋳造できるようにするため、このメソッドはCAUTIONで実装する必要があります。

詳細については、OpenZeppelinの例 [ERC20Mintable.sol](https://github.com/OpenZeppelin/openzeppelin-solidity/blob/v2.3.0/contracts/token/ERC20/ERC20Mintable.sol) をご覧ください。

