# 4. Klaystagramスマートコントラクトを書く <a id="4-write-klaystagram-smart-contract"></a>

1\) 背景  
2\) コントラクト設定  
3\) イベントとデータ構造の設定  
4\) 関数の書き込み

* `写真をアップロード`
* `譲渡所有者`
* `getPhoto`

## 1\) 背景 <a id="1-background"></a>

「Klaystagram」というシンプルなコントラクトを作ります。

* `PhotoData` 構造体は、さまざまな写真データを格納するように定義されている。
* ユーザーは `uploadPhoto` と `transferOwnership` の機能を使用して写真をアップロードし、所有者の写真を転送できます。

## 2\) コントラクトのセットアップ <a id="2-contract-setup"></a>

* ソリッドバージョンを指定してください。 0.5.6 安定版を使用することをお勧めします。
* ERC721規格を活用して、ノンファンジブルトークンを構築します。
  * インポート `ERC721.sol` and `ERC721Enumerable.sol`
  * ERC721についての詳細情報は、 [erc721.org](http://erc721.org) をご覧ください。

```text
pragma solidity 0.5.6;

import "./ERC721/ERC721.sol";
import "./ERC721Enumerable.sol";

contract Kc721, ERC721Enumerable {
```

## 3\) イベントとデータ構造を設定 <a id="3-set-events-and-data-structure"></a>

ブロックチェーン上のアクティビティを追跡するために、イベントを設定する必要があります。

データ構造については、マッピング `_photoList` は uint256 `tokenId` を取得し、特定の `PhotoData` 構造体をマップします。 PhotoUploaded イベントを定義することで、トランザクションレシートは、これを含む関数が呼び出されるたびにこのイベントを記録します。

```text
event PhotoUploaded (uint indexed tokenId, bytes photo, string title, string location, string description, uint256 timestamp);

mapping (uint256 => PhotoData) private _photoList;

struct PhotoData {
    uint256 tokenId;                       // Unique token id, starts from 1 and increases by 1
    address[] ownerHistory;                // History of all previous owners
    bytes photo;                           // Image source
    string title;                          // Title of photo
    string location;                       // Location where photo is taken
    string description;                    // Short description about the photo
    uint256 timestamp;                     // Uploaded time
}
```

## 4\) 関数を書く <a id="4-write-functions"></a>

コントラクトと対話する関数をいくつか書いてみましょう。 このチュートリアルでは、 `uploadPhoto` と `transferOwnership` の 2 つの関数のみを考慮しましょう。 Klaystagram.sol をチェックして、関数のセット全体を確認してください。

### `写真をアップロード` <a id="uploadphoto"></a>

`uploadPhoto` 関数は、写真の画像ソースを含む4つの引数をとります。 シンプルにするため、 `tokenId` は 1 から始まり、1だけ増加します。

`_mint` 関数は ERC721 コントラクトからです。 新しいトークンを作成し、特定のアドレスに割り当てます。この場合、 `msg.sender` です。 このアプリケーションでは、ログインしたユーザーは独自の秘密鍵でトランザクションを作成します。 ですから、 `msg.sender` がユーザーの公開アドレスになります。

最後に、 `PhotoData` 構造体を初期化し、 `_photoList` マッピング内で見つけ、所有者アドレスを `ownerHistory` 配列にプッシュします。 そして、私たちが作成したイベントを放出することを忘れないでください。 上記のように、このイベントはトランザクション領収書に含まれます。

```text
function uploadPhoto(bytes memory photo, string memory title, string memory location, string memory description) public {
    uint256 tokenId = totalSupply() + 1;

    _mint(msg.sender, tokenId);

    address[] memory ownerHistory;

    PhotoData memory newPhotoData = PhotoData({
        tokenId : tokenId,
        ownerHistory : ownerHistory,
        photo : photo,
        title: title,
        location : location,
        description : description,
        timestamp : now
    });

    _photoList[tokenId] = newPhotoData;
    _photoList[tokenId].ownerHistory.push(msg.sender);

    emit PhotoUploaded(tokenId, photo, title, location, description, now);
}
```

### `譲渡所有権` <a id="transferownership"></a>

`transferOwnership` 関数を見てみましょう。 写真の所有権を移転する際には、2つのことを行う必要があります。 まず、所有者を再割り当てしなければならず、新しい所有者アドレスを `ownerHistory` の配列にプッシュする必要があります。

これを行うには、 `transferOwnership` が最初に `safeTransferFrom` 関数を ERC721 標準から呼び出し、最終的には `transferFrom` 関数を呼び出します。 As mentioned above, right after token transfer is successfully done, we have to push new owner information into `ownerHistory` array, and that is exactly why `transferFrom` is overridden as below.

```text
/**
  * @notice safeTransferFrom 関数は、受信側が ERC721 トークンを処理できるかどうかをチェックします。
  * トークンが失われる可能性は低くなります。 After checking is done, it will call transferFrom function defined below
  */
function transferOwnership(uint256 tokenId, address to) public returns(uint, address, address, address) {
    safeTransferFrom(msg.sender, to, tokenId);
    uint ownerHistoryLength = _photoList[tokenId].ownerHistory.length;
    return (
        _photoList[tokenId].tokenId,
        //original owner
        _photoList[tokenId].ownerHistory[0],
        //previous owner, length cannot be less than 2
        _photoList[tokenId].ownerHistory[ownerHistoryLength-2],
        //current owner
        _photoList[tokenId].ownerHistory[ownerHistoryLength-1]);
}

/**
  * @notice Recommend using transferOwnership, which uses safeTransferFrom function
  * @dev Override transferFrom function to make sure that every time ownership transfers
  *  new owner address gets pushed into ownerHistory array
  */
function transferFrom(address from, address to, uint256 tokenId) public {
    super.transferFrom(from, to, tokenId);
    _photoList[tokenId].ownerHistory.push(to);
}
```

### `getPhoto` <a id="getphoto"></a>

最後に、スマートコントラクトに格納されているデータを取得するgetter関数を作りましょう。 単一の関数を呼び出すことで、特定の写真に関するすべての情報を取得します。 ですから、 `getPhoto` 関数は、index\(token id\) を引数として取り、PhotoData 構造体内のすべての要素を返します。

```text
function getPhoto(uint tokenId) public view
returns(uint256, address[] memory, bytes memory, string memory, string memory, string memory, uint256) {
    require(_photoList[tokenId].tokenId != 0, "Photo does not exist");
    return (
        _photoList[tokenId].tokenId,
        _photoList[tokenId].ownerHistory,
        _photoList[tokenId].photo,
        _photoList[tokenId].title,
        _photoList[tokenId].location,
        _photoList[tokenId].description,
        _photoList[tokenId].timestamp);
}
```

これで、今すぐこの契約を展開できます！

